hansfriese

high

# `Boosted3TokenPoolUtils._getTimeWeightedPrimaryBalance()` might return a wrong result.

## Summary
`Boosted3TokenPoolUtils._getTimeWeightedPrimaryBalance()` calculates the `primaryAmount` for a given `bptAmount`.

Currently, it calculates the `primaryAmount` using 1 BPT and gets the final `primaryAmount` by multiplying `bptAmount`.

But during the calculation using a `LinearMath` library, we can't say the result is linear and the result would be different from the real value that we calculate using a real `bptAmount` instead of 1 BPT.

## Vulnerability Detail
`Boosted3TokenPoolUtils._getTimeWeightedPrimaryBalance()` calculates the `primaryAmount` using 1 BPT and multiplies `bptAmount` for the final result like below.

```solidity
File: 2022-12-notional-hansfriese\contracts\vaults\balancer\internal\pool\Boosted3TokenPoolUtils.sol
325:         // NOTE: For Boosted 3 token pools, the LP token (BPT) is just another
326:         // token in the pool. So, we first use _calcTokenOutGivenExactBptIn
327:         // to calculate the value of 1 BPT. Then, we scale it to the BPT
328:         // amount to get the value in terms of the primary currency.
329:         // Use virtual total supply and zero swap fees for joins
330:         uint256 linearBPTAmount = StableMath._calcTokenOutGivenExactBptIn({
331:             amp: oracleContext.ampParam, 
332:             balances: balances, 
333:             tokenIndex: 0, // Primary index
334:             bptAmountIn: BalancerConstants.BALANCER_PRECISION, // 1 BPT 
335:             bptTotalSupply: virtualSupply, 
336:             swapFeePercentage: oracleContext.swapFeePercentage, 
337:             currentInvariant: invariant
338:         });
339: 
340:         // Downscale BPT out
341:         linearBPTAmount = linearBPTAmount * BalancerConstants.BALANCER_PRECISION / poolContext.basePool.primaryScaleFactor;
342: 
343:         // Primary underlying pool = index 0
344:         primaryAmount = _getUnderlyingMainOut(oracleContext.underlyingPools[0], linearBPTAmount);
345: 
346:         uint256 primaryPrecision = 10 ** poolContext.basePool.primaryDecimals;
347:         primaryAmount = (primaryAmount * bptAmount * primaryPrecision) / BalancerConstants.BALANCER_PRECISION_SQUARED; //@audit the result is linear?
348:     }
```

And `_getUnderlyingMainOut()` calls `LinearMath._calcMainOutPerBptIn` for that.

```solidity
File: 2022-12-notional-hansfriese\contracts\vaults\balancer\internal\math\LinearMath.sol
17:     function _calcMainOutPerBptIn(
18:         uint256 bptIn,
19:         uint256 mainBalance,
20:         uint256 wrappedBalance,
21:         uint256 bptSupply,
22:         Params memory params
23:     ) internal pure returns (uint256) {
24:         // Amount out, so we round down overall.
25: 
26:         uint256 previousNominalMain = _toNominal(mainBalance, params);
27:         uint256 invariant = _calcInvariant(previousNominalMain, wrappedBalance);
28:         uint256 deltaNominalMain = Math.divDown(Math.mul(invariant, bptIn), bptSupply);
29:         uint256 afterNominalMain = previousNominalMain.sub(deltaNominalMain);
30:         uint256 newMainBalance = _fromNominal(afterNominalMain, params);
31:         return mainBalance.sub(newMainBalance);
32:     }
```

But we can't say `_calcMainOutPerBptIn` returns a linear `mainout` for `bptIn`.

After the calculation of `afterNominalMain` using `bptIn`, it uses `_fromNominal()` but `_fromNominal()` charges different fees like below.

```solidity
File: 2022-12-notional-hansfriese\contracts\vaults\balancer\internal\math\LinearMath.sol
72:     function _fromNominal(uint256 nominal, Params memory params) internal pure returns (uint256) {
73:         // Since real = nominal + fees, rounding down fees is equivalent to rounding down real.
74: 
75:         if (nominal < params.lowerTarget) {
76:             return (nominal.add(params.fee.mulDown(params.lowerTarget))).divDown(FixedPoint.ONE.add(params.fee));
77:         } else if (nominal <= params.upperTarget) {
78:             return nominal;
79:         } else {
80:             return (nominal.sub(params.fee.mulDown(params.upperTarget)).divDown(FixedPoint.ONE.sub(params.fee)));
81:         }
82:     }
```

So if `nominal` is changed, the different fee formula would be used. Also, the calculation formulas when `nominal < params.lowerTarget` or `nominal > params.upperTarget` don't seem to be linear.

It means `_calcMainOutPerBptIn()` wouldn't return `2 * mainout` for `2 * bptIn` when it returns `mainout` for `bptIn`.

As a result, `Boosted3TokenPoolUtils._getTimeWeightedPrimaryBalance()` might return a wrong result.

## Impact
The calculation logic of `Boosted3TokenPoolUtils._getTimeWeightedPrimaryBalance()` seems to be wrong and the `primaryAmount` would be calculated wrongly.

## Code Snippet
https://github.com/sherlock-audit/2022-12-notional/blob/main/contracts/vaults/balancer/internal/pool/Boosted3TokenPoolUtils.sol#L347

## Tool used
Manual Review

## Recommendation
Recommend calculating `primaryAmount` from the inputted `bptAmount` directly without using `1 BPT`.