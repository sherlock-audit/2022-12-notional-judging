hansfriese

medium

# DOS: `LinearMath._calcBptOutPerMainIn()` might revert for some cases.

## Summary
`LinearMath._calcBptOutPerMainIn()` might revert with zero division for some edge cases.

## Vulnerability Detail
`LinearMath._calcBptOutPerMainIn()` calculates `bptOut` per `mainIn` like below.

```solidity
    function _calcBptOutPerMainIn(
        uint256 mainIn,
        uint256 mainBalance,
        uint256 wrappedBalance,
        uint256 bptSupply,
        Params memory params
    ) internal pure returns (uint256) {
        // Amount out, so we round down overall.

        if (bptSupply == 0) {
            // BPT typically grows in the same ratio the invariant does. The first time liquidity is added however, the
            // BPT supply is initialized to equal the invariant (which in this case is just the nominal main balance as
            // there is no wrapped balance).
            return _toNominal(mainIn, params);
        }

        uint256 previousNominalMain = _toNominal(mainBalance, params);
        uint256 afterNominalMain = _toNominal(mainBalance.add(mainIn), params);
        uint256 deltaNominalMain = afterNominalMain.sub(previousNominalMain);
        uint256 invariant = _calcInvariant(previousNominalMain, wrappedBalance);
        return Math.divDown(Math.mul(bptSupply, deltaNominalMain), invariant); //@audit revert when invariant = 0
    }
```

But `invariant` might be 0 for some edge cases and `_calcBptOutPerMainIn()` might revert with zero division error.

1. Let's assume `wrappedBalance = 0`.
2. Let's assume `params.lowerTarget = 110, real = 10, params.fee = 10%` when it calculates `previousNominalMain` using `_toNominal()`.

```solidity
    function _toNominal(uint256 real, Params memory params) private pure returns (uint256) {
        // Fees are always rounded down: either direction would work but we need to be consistent, and rounding down
        // uses less gas.

        if (real < params.lowerTarget) {
            uint256 fees = (params.lowerTarget - real).mulDown(params.fee);
            return real.sub(fees);
        } else if (real <= params.upperTarget) {
            return real;
        } else {
            uint256 fees = (real - params.upperTarget).mulDown(params.fee);
            return real.sub(fees);
        }
    }
```
3. Then `_toNominal()` will return 0 for the above inputs and `invariant` will be 0 in `_calcBptOutPerMainIn()`.
4. As a result, `_calcBptOutPerMainIn()` will revert with 0 division.

## Impact
Several functions calling `_calcBptOutPerMainIn()` might revert for some cases.

## Code Snippet
https://github.com/sherlock-audit/2022-12-notional/blob/main/contracts/vaults/balancer/internal/math/LinearMath.sol#L54

## Tool used
Manual Review

## Recommendation
I think we should change the calculation logic when `invariant = 0` in `_calcBptOutPerMainIn()` so that it works smoothly for any cases.