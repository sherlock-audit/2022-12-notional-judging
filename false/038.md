ck

high

# StableMath::_calcSpotPrice calculation seems wrong according to code comments

## Summary

According to the doumentation: "The vault may not be able to fully exit its position prior to settlement without incurring excessive slippage and would need to emergency exit its position before liquidity disappears". 

This means that a high slippage may have to be set for an emergency settlement. `The settleVaultEmergency()` however uses the same `_executeSettlement()` function as the `settleVault` function. Instead of using `emergencySettlementSlippageLimitPercent` as should be the case for an emergency, the `balancerPoolSlippageLimitPercent` will still be used and may prevent emergency settlement.

## Vulnerability Detail

https://github.com/notional-finance/leveraged-vaults/blob/071e16d77daaa68dc4cab80a3efe650164f411ec/contracts/vaults/balancer/external/Boosted3TokenAuraHelper.sol#L87-L116

```solidity
function settleVaultEmergency(
        Boosted3TokenAuraStrategyContext calldata context, 
        uint256 maturity, 
        bytes calldata data
    ) external {
        RedeemParams memory params = SettlementUtils._decodeParamsAndValidate(
            context.baseStrategy.vaultSettings.emergencySettlementSlippageLimitPercent,
            data
        );

        uint256 bptToSettle = context.baseStrategy._getEmergencySettlementParams({
            maturity: maturity, 
            totalBPTSupply: context.poolContext._getVirtualSupply(context.oracleContext)
        });

        uint256 redeemStrategyTokenAmount 
            = context.baseStrategy._convertBPTClaimToStrategyTokens(bptToSettle);
        
        _executeSettlement({
            strategyContext: context.baseStrategy,
            oracleContext: context.oracleContext,
            poolContext: context.poolContext,
            maturity: maturity,
            bptToSettle: bptToSettle,
            redeemStrategyTokenAmount: redeemStrategyTokenAmount,
            params: params
        });

        emit BalancerEvents.EmergencyVaultSettlement(maturity, bptToSettle, redeemStrategyTokenAmount);
    }
```

At the end of the `settleVaultEmergency()` function `_executeSettlement()` is called to complete the settlement logic.

https://github.com/notional-finance/leveraged-vaults/blob/071e16d77daaa68dc4cab80a3efe650164f411ec/contracts/vaults/balancer/external/Boosted3TokenAuraHelper.sol#L118-L146

```solidity
function _executeSettlement(
        StrategyContext calldata strategyContext,
        BoostedOracleContext calldata oracleContext,
        ThreeTokenPoolContext calldata poolContext,
        uint256 maturity,
        uint256 bptToSettle,
        uint256 redeemStrategyTokenAmount,
        RedeemParams memory params
    ) private {        
        // Calculate minPrimary using Chainlink oracle data
        params.minPrimary = poolContext._getTimeWeightedPrimaryBalance(
            oracleContext, strategyContext, bptToSettle
        );
        params.minPrimary = params.minPrimary * strategyContext.vaultSettings.balancerPoolSlippageLimitPercent / 
            uint256(BalancerConstants.VAULT_PERCENT_BASIS);

        int256 expectedUnderlyingRedeemed = poolContext._convertStrategyToUnderlying({
            strategyContext: strategyContext,
            oracleContext: oracleContext,
            strategyTokenAmount: redeemStrategyTokenAmount
        });

        strategyContext._executeSettlement({
            maturity: maturity,
            expectedUnderlyingRedeemed: expectedUnderlyingRedeemed,
            redeemStrategyTokenAmount: redeemStrategyTokenAmount,
            params: params
        });
    }
```

Instead of `emergencySettlementSlippageLimitPercent` being used in the `params.minPrimary ` calculation, `balancerPoolSlippageLimitPercent` is used which could to failure of settlement.

## Impact

In an emergency, the settlement may fail due to the wrong slippage being used.

## Code Snippet

https://github.com/notional-finance/leveraged-vaults/blob/071e16d77daaa68dc4cab80a3efe650164f411ec/contracts/vaults/balancer/external/Boosted3TokenAuraHelper.sol#L87-L116
https://github.com/notional-finance/leveraged-vaults/blob/071e16d77daaa68dc4cab80a3efe650164f411ec/contracts/vaults/balancer/external/Boosted3TokenAuraHelper.sol#L118-L146

## Tool used

Manual Review

## Recommendation

Use `emergencySettlementSlippageLimitPercent` instead of `balancerPoolSlippageLimitPercent`  in the _executeSettlement if the call is from `settleVaultEmergency()`. 